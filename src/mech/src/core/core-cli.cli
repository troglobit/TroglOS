# SPDX-License-Identifier: Apache-2.0
CLICON_MODE="exec";
CLICON_PROMPT="%U@%H:%w> ";
CLICON_PLUGIN="core-cli";

# Autocli syntax tree operations, from clixon's "main" example
edit @datamodelmode, cli_auto_edit("basemodel");       # XXX: remove in favor of configure
configure @datamodelmode, cli_auto_edit("basemodel");  # XXX: should edit 'native' candiate
up, cli_auto_up("basemodel");      # XXX: remove
end, cli_auto_up("basemodel");
top, cli_auto_top("basemodel");    # XXX: remove
leave, cli_auto_top("basemodel");  # XXX: should leave configure mode and validate candiate
set @datamodel, cli_auto_set();
merge @datamodel, cli_auto_merge();
create @datamodel, cli_auto_create();
no("Delete a configuration item") {
      @datamodel, cli_auto_del();
      all("Delete whole candidate configuration"), delete_all("candidate");
}
validate("Validate changes"), cli_validate();
commit("Commit the changes"), cli_commit(); {
  [persist-id("Specify the 'persist' value of a previous confirmed-commit") <persist-id-val:string show:"string">("The 'persist' value of the persistent confirmed-commit")], cli_commit(); {
    <cancel:string keyword:cancel>("Cancel an ongoing confirmed-commit"), cli_commit();
    <confirmed:string keyword:confirmed>("Require a confirming commit") {
       [persist("Make this confirmed-commit persistent") <persist-val:string show:"string">("The value that must be provided as 'persist-id' in the confirming-commit or cancel-commit")]
       [<timeout:uint32 range[1:4294967295] show:"1..4294967295">("The rollback timeout in seconds")], cli_commit();
    }
  }
}
copy("Copy and create a new object") {
    running("Copy from running db")  startup("Copy to startup config"), db_copy("running", "startup");
    interface("Copy interface"){
	(<name:string>|<name:string expand_dbvar("candidate","/ietf-interfaces:interfaces/interface=%s/name")>("name of interface to copy from")) to("Copy to interface") <toname:string>("Name of interface to copy to"), cli_copy_config("candidate","//interface[%s='%s']","urn:ietf:params:xml:ns:yang:ietf-interfaces","name","name","toname");
    }
}

debug("Debugging parts of the system"){
    cli("Set cli debug")	 <level:int32>("Set debug level (0..n)"), cli_debug_cli();
    backend("Set backend debug") <level:int32>("Set debug level (0..n)"), cli_debug_backend();
    restconf("Set restconf debug") <level:int32>("Set debug level (0..n)"), cli_debug_restconf();
}

discard("Discard edits (rollback 0)"), discard_changes();

exit("Exit"), cli_quit();
quit("Exit"), hide, cli_quit();

follow("Continuously monitor a log file, default syslog"),   cli_exec_log(); {
    <fn:string logfiles()>(""),                              cli_exec_log();
}

help("Show help"), cli_help();

load("Load configuration from XML file") <filename:string>("Filename (local filename)"),load_config_file("filename", "replace");{
    replace("Replace candidate with file contents"), load_config_file("filename", "replace");{
	cli("Replace candidate with file containing CLI commands"), load_config_file("filename", "replace", "cli");
	xml("Replace candidate with file containing XML"), load_config_file("filename", "replace", "xml");
	json("Replace candidate with file containing JSON"), load_config_file("filename", "replace", "json");
	text("Replace candidate with file containing TEXT"), load_config_file("filename", "replace", "text");
    }
    merge("Merge file with existent candidate"), load_config_file("filename", "merge");{
	cli("Merge candidate with file containing CLI commands"), load_config_file("filename", "merge", "cli");
	xml("Merge candidate with file containing XML"), load_config_file("filename", "merge", "xml");
	json("Merge candidate with file containing JSON"), load_config_file("filename", "merge", "json");
	text("Merge candidate with file containing TEXT"), load_config_file("filename", "merge", "text");
    }
}
save("Save candidate configuration to XML file") <filename:string>("Filename (local filename)"), save_config_file("candidate","filename", "xml");{
    cli("Save configuration as CLI commands"), save_config_file("candidate","filename", "cli");
    xml("Save configuration as XML"), save_config_file("candidate","filename", "xml");
    json("Save configuration as JSON"), save_config_file("candidate","filename", "json");
    text("Save configuration as TEXT"), save_config_file("candidate","filename", "text");
    netconf("Save configuration as NETCONF"), save_config_file("candidate","filename", "netconf");
}

show("Show various system state and configuration") {
	system-information("System information"),            cli_exec_show();
	fdb("Show forwarding database (unicast)"),           cli_exec_show();
	mdb("Show multicast forwarding database (unicast)"), cli_exec_show();
	ports("Show ports available for bridging"),          cli_exec_show();
	vlans("Show port groups in bridge"),                 cli_exec_show();
	ifaces("Show interfaces and their addresses"),       cli_exec_show();
	log("Show latest syslog entries"),                   cli_exec_log(); {
		<fn:string logfiles()>(""),                  cli_exec_log();
	}
	version("Show operating system version"),            cli_exec_show();

	auto("Show expand x") {
		xml @datamodelshow, cli_show_auto("candidate", "xml", true, false, "report-all");
		text @datamodelshow, cli_show_auto("candidate", "text", true, false, "report-all");
		json @datamodelshow, cli_show_auto("candidate", "json", true, false, "report-all");
		netconf @datamodelshow, cli_show_auto("candidate", "netconf", true, false, "report-all");
		cli @datamodelshow, cli_show_auto("candidate", "cli", true, false, "report-all", "set ");
	}

	compare("Compare candidate and running databases"), compare_dbs((int32)0);{
		xml("Show comparison in xml"), compare_dbs((int32)0);
		text("Show comparison in text"), compare_dbs((int32)1);
	}

	configuration("Show configuration"), cli_show_auto_mode("candidate", "text", true, false); {
		xml("Show configuration as XML"), cli_show_auto_mode("candidate", "xml", true, false); {
			default("With-default mode") {
				report-all, cli_show_auto_mode("candidate", "xml", true, false, "report-all");
				trim, cli_show_auto_mode("candidate", "xml", true, false, "trim");
				explicit, cli_show_auto_mode("candidate", "xml", true, false, "explicit");
				report-all-tagged, cli_show_auto_mode("candidate", "xml", true, false, "report-all-tagged");
				report-all-tagged-default, cli_show_auto_mode("candidate", "xml", true, false, "report-all-tagged-default");
				report-all-tagged-strip, cli_show_auto_mode("candidate", "xml", true, false, "report-all-tagged-strip");
			}
		}
		cli("Show configuration as CLI commands"), cli_show_auto_mode("candidate", "cli", true, false, "explicit", "set ");
		netconf("Show configuration as netconf edit-config operation"), cli_show_auto_mode("candidate", "netconf", true, false);
		text("Show configuration as text"), cli_show_auto_mode("candidate", "text", true, false);
		json("Show configuration as JSON"), cli_show_auto_mode("candidate", "json", true, false);
	}
	state("Show configuration and state"), cli_show_auto_mode("running", "text", true, true); {
		xml("Show configuration and state as XML"), cli_show_auto_mode("running", "xml", true, true); {
			default("With-default mode") {
				report-all, cli_show_auto_mode("running", "xml", true, true, "report-all");
				trim, cli_show_auto_mode("running", "xml", true, true, "trim");
				explicit, cli_show_auto_mode("running", "xml", true, true, "explicit");
				report-all-tagged, cli_show_auto_mode("running", "xml", true, true, "report-all-tagged");
				report-all-tagged-default, cli_show_auto_mode("running", "xml", true, true, "report-all-tagged-default");
				report-all-tagged-strip, cli_show_auto_mode("running", "xml", true, true, "report-all-tagged-strip");
			}
		}
	}
	yang("Show yang specs"), show_yang(); {
		clixon-example("Show clixon-example yang spec"), show_yang("clixon-example");
	}
}

# Hidden shell command, after 'show' command to avoid any possible completion problems
shell("Start domain shell"), hide, cli_exec_shell();{
  <source:rest>("Single shell command"), cli_exec_shell();
}
